apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.qwe7002.telegram_sms"
        minSdkVersion 22
        //noinspection OldTargetApi
        targetSdkVersion 30
        versionCode System.getenv("VERSION_CODE") == null ? 1 : System.getenv("VERSION_CODE").toInteger()
        versionName System.getenv("VERSION_NAME") == null ? "Debug": System.getenv("VERSION_NAME").toString()
        ndk{
            abiFilters "armeabi-v7a", "arm64-v8a"
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    signingConfigs {
        release {
            storeFile file("keys.jks")
            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'com.squareup.okio:okio:3.0.0-alpha.9'
    implementation 'com.squareup.okhttp3:okhttp:5.0.0-alpha.2'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:5.0.0-alpha.2'
    implementation 'org.conscrypt:conscrypt-android:2.5.2'
    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'io.github.pilgr:paperdb:2.7.1'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.6.21'
}

task copy_language_pack(type: Copy) {
    description 'Copy the language pack to the source directory, replace the placeholder file'
    from 'language_pack/'
    into 'src/main/res/'
    exclude '**/README.md'
    include '**/*'
}
