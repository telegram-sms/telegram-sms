apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
android {
    compileSdkVersion 30
    defaultConfig {
        applicationId "com.qwe7002.telegram_sms"
        minSdkVersion 22
        targetSdkVersion 30
        versionCode System.getenv("VERSION_CODE") == null ? 1 : System.getenv("VERSION_CODE").toInteger()
        versionName System.getenv("VERSION_NAME") == null ? "Debug": System.getenv("VERSION_NAME").toString()
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    signingConfigs {
        release {
            storeFile file("keys.jks")
            storePassword project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.browser:browser:1.3.0'
    implementation 'com.squareup.okio:okio:2.9.0'
    implementation 'com.squareup.okhttp3:okhttp:4.9.0'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:4.9.0'
    implementation 'org.conscrypt:conscrypt-android:2.5.1'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'io.paperdb:paperdb:2.7.1'
    implementation 'me.dm7.barcodescanner:zxing:1.9.13'
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.30'
}

repositories {
    mavenCentral()
}

task copy_language_pack(type: Copy) {
    description 'Copy the language pack to the source directory, replace the placeholder file'
    from 'language_pack/'
    into 'src/main/res/'
    exclude '**/README.md'
    include '**/*'
}
