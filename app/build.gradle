// For getting git information
def getGitBranch() {
    // 优先从 GitLab CI 的环境变量中获取分支名
    // Prioritize getting the branch name from the GitLab CI environment variable.
    def ciBranch = System.getenv("CI_COMMIT_REF_NAME")
    if (ciBranch != null && !ciBranch.isEmpty()) {
        println "Found CI branch: " + ciBranch
        return ciBranch
    }

    // 如果不在 CI 环境中（比如在本地），则回退到使用 git 命令
    // If not in a CI environment (e.g., locally), fall back to using the git command.
    try {
        def localBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
        println "Found local branch: " + localBranch
        return localBranch
    } catch (e) {
        println "Could not determine branch, falling back to 'unknown-branch'"
        return "unknown-branch"
    }
}


// Get the branch name before configuring Android, so we can use it directly later.
def currentBranch = getGitBranch()


apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
android {
    compileSdk = 36

    defaultConfig {
        applicationId = 'com.qwe7002.telegram_sms'
        minSdk = 23
        targetSdk = 34

        versionCode = System.getenv("VERSION_CODE") == null ? 1 : System.getenv("VERSION_CODE").toInteger()
        versionName = System.getenv("VERSION_NAME") == null ? "Debug" : System.getenv("VERSION_NAME").toString()
    }

    compileOptions {

        sourceCompatibility = 17
        targetCompatibility = 17
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    androidResources {
        generateLocaleConfig = true
    }

    signingConfigs {
        release {
            storeFile = file("keys.jks")
            storePassword = project.hasProperty("KEYSTORE_PASS") ? KEYSTORE_PASS : System.getenv("KEYSTORE_PASS")
            keyAlias = project.hasProperty("ALIAS_NAME") ? ALIAS_NAME : System.getenv("ALIAS_NAME")
            keyPassword = project.hasProperty("ALIAS_PASS") ? ALIAS_PASS : System.getenv("ALIAS_PASS")
            v1SigningEnabled = true
            v2SigningEnabled = true
        }
    }
    buildTypes {
        release {
            minifyEnabled = false

            if (file("keys.jks").exists()) {
                signingConfig = signingConfigs.release
            } else {
                println "喵呜！本地没有 keys.jks，借用 Debug 签名来跑 Release 包！"
                signingConfig = signingConfigs.debug
            }

            ndk {
                //noinspection ChromeOsAbiSupport
                // abiFilters 比较特殊，它通常接受一个集合。
                // 如果报错，试着保持原样，或者用 abiFilters.addAll(...)
                // 但如果是属性赋值警告，通常是因为它被当成了方法。
                // 在 Groovy DSL 中，ndk 块里的 abiFilters 往往不需要改，
                // 但如果它报错，可以尝试 abiFilters = [...]
                abiFilters "armeabi-v7a", "arm64-v8a"
            }

            if (currentBranch == "nightly") {
                applicationIdSuffix = ".nightly"
                resValue "string", "app_name", "Telegram SMS (Nightly)"
            }
        }

        debug {
            applicationIdSuffix = ".debug"
            resValue "string", "app_name", "Telegram SMS (Debug)"
        }
    }
    namespace = 'com.qwe7002.telegram_sms'
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.browser:browser:1.9.0'
    implementation 'com.squareup.okio:okio:3.16.4'
    implementation 'com.squareup.okhttp3:okhttp:5.3.2'
    implementation 'com.squareup.okhttp3:okhttp-dnsoverhttps:5.3.2'
    implementation 'org.conscrypt:conscrypt-android:2.5.3'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'com.tencent:mmkv:2.3.0'
    implementation 'com.github.yuriy-budiyev:code-scanner:2.1.0'
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:2.2.21'
    implementation 'androidx.activity:activity-ktx:1.12.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'com.goterl:lazysodium-android:5.2.0@aar'
    implementation 'net.java.dev.jna:jna:5.18.1@aar'
}

tasks.register('copy_language_pack', Copy) {
    description 'Copy the language pack to the source directory, replace the placeholder file'
    from 'language_pack/'
    into 'src/main/res/'
    exclude '**/README.md'
    include '**/*'
}
